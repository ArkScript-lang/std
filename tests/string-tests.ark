(import std.String)
(import std.Testing)

(test:suite string {
  (test:case "find" {
    (test:eq (builtin__string:find "abcd" "a") (string:find "abcd" "a"))
    (test:eq (builtin__string:find "abcd" "e") (string:find "abcd" "e"))
    (test:eq (builtin__string:find "aaacd" "a" 1) (string:findAfter "aaacd" "a" 1)) })

  (test:case "removeAt, setAt" {
    (test:eq (builtin__string:removeAt "abcd" 1) (string:removeAt "abcd" 1))
    (test:eq (builtin__string:removeAt "abcd" -1) "abc")
    (test:eq (builtin__string:setAt "abcd" 1 "z") (string:setAt "abcd" 1 "z")) })

  (test:case "contains?" {
    (test:expect (string:contains? "hello, world" "h"))
    (test:expect (string:contains? "hello, world" "hello"))
    (test:expect (string:contains? "hello, world" "world"))
    (test:expect (string:contains? "hello, world" ","))
    (test:expect (not (string:contains? "hello, world" "!"))) })

  (test:case "containsAnyOf?" {
    (test:expect (string:containsAnyOf? "Hello, world" ["world" "one"]))
    (test:expect (string:containsAnyOf? "Hello, world" string:punctuation))
    (test:expect (not (string:containsAnyOf? "Hello, world" [])))
    (test:expect (not (string:containsAnyOf? "Hello, world" ["nice" "cool"]))) })

  (test:case "emptyOrWhitespace?" {
    (test:eq (string:emptyOrWhitespace? "") true)
    (test:eq (string:emptyOrWhitespace? " ") true)
    (test:eq (string:emptyOrWhitespace? "   ") true)
    (test:eq (string:emptyOrWhitespace? "\t") true)
    (test:eq (string:emptyOrWhitespace? "\n") true)
    (test:eq (string:emptyOrWhitespace? "\r") true)
    (test:eq (string:emptyOrWhitespace? " \t\r\n") true)
    (test:eq (string:emptyOrWhitespace? " \t a") false)
    (test:eq (string:emptyOrWhitespace? "a") false)
    (test:eq (string:emptyOrWhitespace? "a ") false)
    (test:eq (string:emptyOrWhitespace? " a ") false) })

  (test:case "ord, chr" {
    (test:eq (builtin__string:ord "a") (string:ord "a"))
    (test:eq (builtin__string:chr 65) (string:chr 65)) })

  (test:case "count" {
    (test:eq (string:count "the three truths" "th") 3)
    (test:eq (string:count "the three truths" "thz") 0)
    (test:eq (string:count "ababababab" "abab") 2) })

  (test:case "toLower, toUpper" {
    (test:eq (string:toLower "ABCDEFGHIJKLMNOPQRSTUVWXYZ") "abcdefghijklmnopqrstuvwxyz")
    (test:eq (string:toLower "abcdefghijklmnopqrstuvwxyz") "abcdefghijklmnopqrstuvwxyz")
    (test:eq (string:toUpper "abcdefghijklmnopqrstuvwxyz") "ABCDEFGHIJKLMNOPQRSTUVWXYZ")
    (test:eq (string:toUpper "ABCDEFGHIJKLMNOPQRSTUVWXYZ") "ABCDEFGHIJKLMNOPQRSTUVWXYZ") })

  (test:case "reverse" {
    (test:eq (string:reverse "dlrow olleh") "hello world" )
    (test:eq (string:reverse "") "")
    (test:eq (string:reverse "a") "a") })

  (test:case "repeat" {
    (test:eq (string:repeat "" 5) "")
    (test:eq (string:repeat "a" 1) "a")
    (test:eq (string:repeat "a" 5) "aaaaa") })

  (test:case "slice" {
    (test:eq (string:slice "hello world" 0 5) "hello")
    (test:eq (string:slice "hello world" 0 100) "hello world")
    (test:eq (string:slice "hello world" 0 1) "h")
    (test:eq (string:slice "hello world" 1 4) "ello") })

  (test:case "split" {
    (test:eq (string:split "a,bc,def" ",") ["a" "bc" "def"])
    (test:eq (string:split "a-;-bc-;-def" "-;-") ["a" "bc" "def"])
    (test:eq (string:split "abcdef" ";;;;") ["abcdef"])
    (test:eq (string:split "a" ",") ["a"])
    (test:eq (string:split "" ",") [""]) })

  (test:case "replace" {
    (test:eq (string:replace "hello world" "coucou" "not") "hello world")
    (test:eq (string:replace "hello world" "hello" "not") "not world")
    (test:eq (string:replace "hello hello " "hello" "not") "not not ")
    (test:eq (string:replace "hello worldABC" "ABC" "worldABC") "hello worldworldABC") })

  (test:case "join" {
  (test:eq (string:join ["hello" 3.14 true "world"] ";") "hello;3.14;true;world")
  (test:eq (string:join ["hello"] ";") "hello")
  (test:eq (string:join [] ";;") "") })

  (test:case "lstrip" {
    (test:eq (string:lstrip "abc") "abc")
    (test:eq (string:lstrip " \t\n\rabc") "abc")
    (test:eq (string:lstrip "abc \t\n\r") "abc \t\n\r")
    (test:eq (string:lstrip "") "")
    (test:eq (string:lstrip " \t\n\r") "") })

  (test:case "rstrip" {
    (test:eq (string:rstrip "abc") "abc")
    (test:eq (string:rstrip "abc \t\n\r") "abc")
    (test:eq (string:rstrip " \t\n\rabc") " \t\n\rabc")
    (test:eq (string:rstrip "") "")
    (test:eq (string:rstrip " \t\n\r") "") })

  (test:case "strip" {
    (test:eq (string:strip "\t\n\r abc \t\n\r") "abc")
    (test:eq (string:strip "\t\n\r ") "")
    (test:eq (string:strip "abc") "abc")
    (test:eq (string:strip "") "")
    (test:eq (string:strip " \t\n\rabc") "abc")
    (test:eq (string:strip "abc \t\n\r") "abc") })

  (test:case "stripMargin" {
    (test:eq (string:stripMargin "") "")
    (test:eq (string:stripMargin "   hello") "   hello")
    (test:eq (string:stripMargin "   |hello") "hello")
    (test:eq
      (string:stripMargin "   |hello
                              |abc")
      "hello\nabc")
  (test:eq
    (string:stripMargin "hello
                        |abc
                        |d")
    "hello\nabc\nd")
  (test:eq
    (string:stripMargin "hello
                        |
                        |abc
                        |d")
    "hello\n\nabc\nd") })

  (test:case "startsWith?" {
    (test:expect (string:startsWith? "hello world" "hello"))
    (test:expect (not (string:startsWith? "hello world" "Hello")))
    (test:expect (not (string:startsWith? "hello world" "world")))
    (test:expect (not (string:startsWith? "hello world" "ello")))
    (test:expect (string:startsWith? "" "")) })

  (test:case "endsWith?" {
      (test:expect (string:endsWith? "hello world" "world"))
      (test:expect (not (string:endsWith? "hello world" "World")))
      (test:expect (not (string:endsWith? "hello world" "hello")))
      (test:expect (not (string:endsWith? "hello world" "worl")))
      (test:expect (string:endsWith? "" "")) })

  (test:case "zfill" {
    (test:eq (string:zfill "" 4) "0000")
    (test:eq (string:zfill "1" 4) "0001")
    (test:eq (string:zfill "11" 4) "0011")
    (test:eq (string:zfill "111" 4) "0111")
    (test:eq (string:zfill "1111" 4) "1111")
    (test:eq (string:zfill "11111" 4) "11111") })

  (test:case "center" {
    (test:eq (string:center "ArkScript" 15) "   ArkScript   ")
    (test:eq (string:center "ArkScript" 14) "  ArkScript   ")
    (test:eq (string:center "ArkScript" 4) "ArkScript") })

  (test:case "removePrefix, removeSuffix" {
    (test:eq (string:removePrefix "TestCase" "Test") "Case")
    (test:eq (string:removePrefix "BaseTestCase" "Test") "BaseTestCase")

    (test:eq (string:removeSuffix "TestCase" "Case") "Test")
    (test:eq (string:removeSuffix "BaseTestCase" "Test") "BaseTestCase") })

  (test:case "levenshteinDistance" {
    (test:eq (string:levenshteinDistance "arkscript" "arkscript") 0)
    (test:eq (string:levenshteinDistance "arkscript" "Orkscript") 1)
    (test:eq (string:levenshteinDistance "arkscript" "0rCscript") 2)
    (test:eq (string:levenshteinDistance "arkscript" "OrC") 8) })
})
