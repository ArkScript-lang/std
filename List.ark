###
# @meta List
# @brief Iterate over a given list and run a given function on every element.
# @details The original list is left unmodified. Example:
# ---
# (import "List.ark")
# (let collection [1 2 5 12])
# (let new (list:forEach collection (fun (element) {
#     (print element)
# })))
# ---
# @param _L the list to iterate over
# @param _func the function to call on each element
# @author https://github.com/SuperFola
###
(let list:forEach (fun (_L _func) {
    (mut _index 0)
    (while (< _index (len _L)) {
        (mut _element (@ _L _index))
        (_func _element)
        (set _index (+ 1 _index))
    })
}))

###
# @meta List
# @brief Iterate over a given list and multiply all the elements with the others.
# @details The original list is left unmodified. Example:
# ---
# (import "List.ark")
# (let collection [1 2 5 12])
# (let p (list:product collection))  # => 120
# ---
# @param _L the list to iterate over
# @author https://github.com/FrenchMasterSword
###
(let list:product (fun (_L) {
    (mut _index 0)
    (mut _output 1)
    (while (< _index (len _L)) {
        (set _output (* _output (@ _L _index)))
        (set _index (+ 1 _index))
    })
    _output
}))

###
# @meta List
# @brief Iterate over a given list and sum all the elements.
# @details The original list is left unmodified. Example:
# ---
# (import "List.ark")
# (let collection [1 2 5 12])
# (let p (list:sum collection))  # => 20
# ---
# @param _L the list to iterate over
# @author https://github.com/FrenchMasterSword
###
(let list:sum (fun (_L) {
    (mut _index 0)
    (mut _output 0)
    (while (< _index (len _L)) {
        (set _output (+ _output (@ _L _index)))
        (set _index (+ 1 _index))
    })
    _output
}))

###
# @meta List
# @brief Drop the first n elements of a list
# @details The original list is left unmodified.
# ---
# (let cool-stuff [1 2 3 4 5 6 7 8 9])
# (print (list:drop cool-stuff 4))  # [5 6 7 8 9]
# ---
# @param _L the list to work on
# @param _n the number of elements to drop
# @author https://github.com/rstefanic
###
(let list:drop (fun (_L _n) {
    (mut _index _n)
    (mut _output [])
    (while (< _index (len _L)) {
        (set _output (append _output (@ _L _index)))
        (set _index (+ 1 _index))
    })
    _output
}))

###
# @meta List
# @brief Keep elements in a given list if they follow a predicate
# @details The original list is left unmodified.
# ---
# (import "Math.ark")
# (print (list:filter [1 2 3 4 5 6 7 8 9] math:even))  # [2 4 6 8]
# ---
# @param _L the list to work on
# @param _f the predicate
# @author https://github.com/rstefanic
###
(let list:filter (fun (_L _f) {
    (mut _index 0)
    (mut _output [])
    (while (< _index (len _L)) {
        (if (_f (@ _L _index))
            (set _output (append _output (@ _L _index))))
        (set _index (+ 1 _index))
    })
    _output
}))

###
# @meta List
# @brief Applies a given function to each element of a list
# @details The original list is left unmodified.
# ---
# (print (list:map [1 2 3 4 5 6 7 8 9] (fun (e) (* e e))))  # [1 4 9 25 36 49 64 81]
# ---
# @param _L the list to work on
# @param _f the function to apply to each element
# @author https://github.com/rstefanic
###
(let list:map (fun (_L _f) {
    (mut _index 0)
    (mut _output [])
    (while (< _index (len _L)) {
        (set _output (append _output (_f (@ _L _index))))
        (set _index (+ 1 _index))
    })
    _output
}))

###
# @meta List
# @brief Apply a function to the elements of a list to reduce it
# @details The original list is left unmodified.
# ---
# (let cool [1 2 3 4 5 6 7 8 9])
# (print (list:reduce cool (fun (a b) (+ a b))))  # 45
# ---
# @param _L the list to work on
# @param _f the function to apply
# @author https://github.com/FrenchMasterSword
###
(let list:reduce (fun (_L _f) {
    (mut _index 1)
    (mut _output (@ _L 0))
    (while (< _index (len _L)) {
        (set _output (_f _output (@ _L _index)))
        (set _index (+ 1 _index))
    })
    _output
}))

(import "Math.ark")  # needed for (math:min a b)

###
# @meta List
# @brief Take the first n elements of
# @details The original list is left unmodified.
# ---
# (print (list:take [1 2 3 4 5 6 7 8 9] 4))  # [1 2 3 4]
# ---
# @param _L the list to work on
# @param _n the number of elements to take
# @author https://github.com/rstefanic
###
(let list:take (fun (_L _n) {
    (mut _index 0)
    (mut _output [])
    (set _n (math:min _n (len _L)))

    (while (< _index _n) {
        (set _output (append _output (@ _L _index)))
        (set _index (+ 1 _index))
    })
    _output
}))

###
# @meta List
# @brief Unzip a list of [[a b] [c d]...] into [[a c ...] [b d ...]]
# @details The original list is left unmodified.
# ---
# (let zipped [[1 5] [2 6] [3 7] [4 8]])
# (print (list:unzip zipped))  # [[1 2 3 4] [5 6 7 8]]
# ---
# @param _L the list to work on
# @author https://github.com/FrenchMasterSword
###
(let list:unzip (fun (_L) {
    (let _m (len _L))
    (mut _list1 [])
    (mut _list2 [])
    (mut _index 0)
    (while (< _index _m) {
        (mut current (@ _L _index))
        (set _list1 (append _list1 (@ current 0)))
        (set _list2 (append _list2 (@ current 1)))
        (set _index (+ 1 _index))
    })
    [_list1 _list2]
}))

###
# @meta List
# @brief Zip two lists into one: [1 2 3 4] and [5 6 7 8] will give [[1 5] [2 6] [3 7] [4 8]]
# @details The original lists are left unmodified.
# ---
# (let a [1 2 3 4])
# (let b [5 6 7 8])
# (print (list:zip a b))  # [[1 5] [2 6] [3 7] [4 8]]
# ---
# @param _a the first list to work on
# @param _b the second list to work on
# @author https://github.com/FrenchMasterSword
###
(let list:zip (fun (_a _b) {
    (let _m (math:min (len _a) (len _b)))
    (mut _c [])
    (mut _index 0)
    (while (< _index _m) {
        (set _c (append _c [(@ _a _index) (@ _b _index)]))
        (set _index (+ 1 _index))
    })
    _c
}))